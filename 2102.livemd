<!-- livebook:{"file_entries":[{"name":"2102.txt","type":"attachment"}]} -->
# Advent of Code 2021 - Day 2

```elixir
Mix.install(
  [
    {:kino_aoc, git: "https://github.com/ljgago/kino_aoc"}
  ],
  force: true
)
```

## Introduction - Dive

Puzzle: [Dive](https://adventofcode.com/2021/day/2)

Now, you need to figure out how to pilot this thing.

It seems like the submarine can take a series of commands like forward 1, down 2, or up 3:

forward X increases the horizontal position by X units.
down X increases the depth by X units.
up X decreases the depth by X units.
Note that since you're on a submarine, down and up affect your depth, and so they have the opposite result of what you might expect.

The submarine seems to already have a planned course (your puzzle input). You should probably figure out where it's going. For example:

```
forward 5
down 5
forward 8
up 3
down 8
forward 2
```

Your horizontal position and depth both start at 0. The steps above would then modify them as follows:

```
forward 5 adds 5 to your horizontal position, a total of 5.
down 5 adds 5 to your depth, resulting in a value of 5.
forward 8 adds 8 to your horizontal position, a total of 13.
up 3 decreases your depth by 3, resulting in a value of 2.
down 8 adds 8 to your depth, resulting in a value of 10.
forward 2 adds 2 to your horizontal position, a total of 15.
```

After following these instructions, you would have a horizontal position of 15 and a depth of 10. (Multiplying these together produces 150.)

Calculate the horizontal position and depth you would have after following the planned course. What do you get if you multiply your final horizontal position by your final depth?

## Parser

<!-- livebook:{"attrs":{"day":"2","session_secret":"SESSION","variable":"puzzle_input","year":"2021"},"chunks":null,"kind":"Elixir.KinoAOC.HelperCell","livebook_object":"smart_cell"} -->

```elixir
{:ok, puzzle_input} =
  KinoAOC.download_puzzle("2021", "2", System.fetch_env!("LB_SESSION"))
```

### Code

```elixir
defmodule Parser do
  def parse(input) do
    input
    |> String.trim()
    |> String.split()
    |> Enum.chunk_every(2, 2)
    |> Enum.into([], fn [a, b] -> %{direction: a, amount: String.to_integer(b)} end)
  end
end
```

### Tests

```elixir
ExUnit.start(autorun: false)

defmodule ParserTest do
  use ExUnit.Case, async: true
  import Parser

  @input "forward 5\ndown 5\nforward 8\nup 3\ndown 8\nforward 2"
  @expected [
    %{direction: "forward", amount: 5},
    %{direction: "down", amount: 5},
    %{direction: "forward", amount: 8},
    %{direction: "up", amount: 3},
    %{direction: "down", amount: 8},
    %{direction: "forward", amount: 2}
  ]

  test "parse test" do
    actual = parse(@input)
    assert actual == @expected
  end
end

ExUnit.run()
```

<!-- livebook:{"branch_parent_index":1} -->

## Part One

### Code

```elixir
defmodule PartOne do
  def resolve(input) do
    IO.puts("--- Part One ---")
    IO.puts("Result: #{run(input)}")
  end

  def run(input) do
    input
    |> Enum.reduce({_depth = 0, _position = 0}, fn
      %{direction: "forward", amount: number}, {depth, position} -> {depth, position + number}
      %{direction: "down", amount: number}, {depth, position} -> {depth + number, position}
      %{direction: "up", amount: number}, {depth, position} -> {depth - number, position}
    end)
    |> then(fn {depth, position} -> depth * position end)
  end
end
```

### Tests

```elixir
ExUnit.start(autorun: false)

defmodule PartOneTest do
  use ExUnit.Case, async: true
  import PartOne

  @input [
    %{direction: "forward", amount: 5},
    %{direction: "down", amount: 5},
    %{direction: "forward", amount: 8},
    %{direction: "up", amount: 3},
    %{direction: "down", amount: 8},
    %{direction: "forward", amount: 2}
  ]
  @expected 150

  test "part one" do
    actual = run(@input)
    assert actual == @expected
  end
end

ExUnit.run()
```

### Solution

```elixir
PartOne.resolve(Parser.parse(puzzle_input))
```

<!-- livebook:{"branch_parent_index":1} -->

## Part Two

Based on your calculations, the planned course doesn't seem to make any sense. You find the submarine manual and discover that the process is actually slightly more complicated.

In addition to horizontal position and depth, you'll also need to track a third value, aim, which also starts at 0. The commands also mean something entirely different than you first thought:

```
down X increases your aim by X units.
up X decreases your aim by X units.
forward X does two things:
It increases your horizontal position by X units.
It increases your depth by your aim multiplied by X.
Again note that since you're on a submarine, down and up do the opposite of what you might expect: "down" means aiming in the positive direction.
```

Now, the above example does something different:

```
forward 5 adds 5 to your horizontal position, a total of 5. Because your aim is 0, your depth does not change.
down 5 adds 5 to your aim, resulting in a value of 5.
forward 8 adds 8 to your horizontal position, a total of 13. Because your aim is 5, your depth increases by 8*5=40.
up 3 decreases your aim by 3, resulting in a value of 2.
down 8 adds 8 to your aim, resulting in a value of 10.
forward 2 adds 2 to your horizontal position, a total of 15. Because your aim is 10, your depth increases by 2*10=20 to a total of 60.
```

After following these new instructions, you would have a horizontal position of 15 and a depth of 60. (Multiplying these produces 900.)

Using this new interpretation of the commands, calculate the horizontal position and depth you would have after following the planned course.

**What do you get if you multiply your final horizontal position by your final depth?**

<!-- livebook:{"break_markdown":true} -->

### Code

```elixir
defmodule PartTwo do
  def resolve(input) do
    IO.puts("--- Part Two ---")
    IO.puts("Result: #{run(input)}")
  end

  def run(input) do
    input
    |> Enum.reduce({_depth = 0, _position = 0, _aim = 0}, fn
      %{direction: "forward", amount: number}, {depth, position, aim} ->
        {depth + aim * number, position + number, aim}

      %{direction: "down", amount: number}, {depth, position, aim} ->
        {depth, position, aim + number}

      %{direction: "up", amount: number}, {depth, position, aim} ->
        {depth, position, aim - number}
    end)
    |> then(fn {depth, position, _} -> depth * position end)
  end
end
```

### Tests

```elixir
ExUnit.start(autorun: false)

defmodule PartTwoTest do
  use ExUnit.Case, async: true
  import PartTwo

  @input [
    %{direction: "forward", amount: 5},
    %{direction: "down", amount: 5},
    %{direction: "forward", amount: 8},
    %{direction: "up", amount: 3},
    %{direction: "down", amount: 8},
    %{direction: "forward", amount: 2}
  ]
  @expected 900

  test "part two" do
    actual = run(@input)
    assert actual == @expected
  end
end

ExUnit.run()
```

### Solution

```elixir
PartTwo.resolve(Parser.parse(puzzle_input))
```

<!-- livebook:{"offset":6933,"stamp":{"token":"QTEyOEdDTQ.eJTKzqD3hmjc35L9Wuqd5Xqmqb6nSkg4zOCzoJ7xm4_JB9LehNK_9qn36cM.VlJZZNOLfiYl6l7k.PfQOaVmU7FNf19K74xBoCOruNy8Rq7Jn7kmUajh76g8Vb8nAyP6PJrWa.fCPhIf4wZD72EN6g4-T29w","version":1}} -->
